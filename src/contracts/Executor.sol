// The Licensed Work is (c) 2022 Sygma
// SPDX-License-Identifier: LGPL-3.0-only
pragma solidity 0.8.11;

import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import "@openzeppelin/contracts/utils/Context.sol";
import "./interfaces/IAccessControlSegregator.sol";


import "./interfaces/IBridge.sol";
import "./interfaces/IHandler.sol";
import "./interfaces/IStateRootStorage.sol";
import "./libraries/StorageProof.sol";



/**
    @title Facilitates proposal executions.
    @author ChainSafe Systems.
 */
contract Executor is Context {
    using ECDSA for bytes32;

    IBridge public immutable _bridge;
    uint8 public immutable _domainID;
    IAccessControlSegregator public _accessControl;

    // originDomainID => slot index number
    mapping(uint8 => uint8) public _slotIndexes;
    // securityModel => state root storage contract address
    mapping(uint8 => address) public _securityModels;
    // origin domainID => nonces set => used deposit nonces
    mapping(uint8 => mapping(uint256 => uint256)) public usedNonces;
    //  origin domainID => router address
    mapping(uint8 => address) public _originDomainIDToRouter;

    struct Proposal {
        uint8 originDomainID;
        uint8 securityModel;
        uint64 depositNonce;
        bytes32 resourceID;
        bytes data;
        bytes[] storageProof;
    }

    event ProposalExecution(uint8 originDomainID, uint64 depositNonce, bytes handlerResponse);
    event FeeRouterChanged(uint8 originDomainID, address newRouter);
    event FailedHandlerExecution(bytes lowLevelData, uint8 originDomainID, uint64 depositNonce);

    error EmptyProposalsArray();
    error BridgeIsPaused();
    error AccessNotAllowed(address sender, bytes4 funcSig);
    error TransferHashDoesNotMatchSlotValue(bytes32 transferHash);

    modifier onlyAllowed() {
        _onlyAllowed(msg.sig, _msgSender());
        _;
    }

    modifier whenBridgeNotPaused() {
        if (_bridge.paused()) revert BridgeIsPaused();
        _;
    }

    function _onlyAllowed(bytes4 sig, address sender) private view {
        if (!_accessControl.hasAccess(sig, sender)) revert AccessNotAllowed(sender, sig);
    }

    constructor(
        address bridge,
        address accessControl,
        uint8 securityModel,
        address stateRootStorage
    ) {
        _bridge = IBridge(bridge);
        _domainID = _bridge._domainID();
        _accessControl = IAccessControlSegregator(accessControl);
        _securityModels[securityModel] = stateRootStorage;
    }

    /**
        @notice Maps the {originDomainID} to {router} in _originDomainIDToRouter.
        @notice Only callable by address that has the right to call the specific function,
        which is mapped in {functionAccess} in AccessControlSegregator contract.
        @param newRouter Address of router that will be updated to.
     */
    function adminChangeRouter(uint8 originDomainID, address newRouter) external onlyAllowed {
        _originDomainIDToRouter[originDomainID] = newRouter;
        emit FeeRouterChanged(originDomainID, newRouter);
    }

    /**
        @notice Maps the {originDomainID} to {slotIndex} in _slotIndexes.
        @notice Only callable by address that has the right to call the specific function,
        which is mapped in {functionAccess} in AccessControlSegregator contract.
        @param originDomainID domain from which the proposal originated.
        @param slotIndex Index number to be used for the belonging origin domain ID in slot key calculation.
     */
    function adminChangeSlotIndex(uint8 originDomainID, uint8 slotIndex) external onlyAllowed {
        _slotIndexes[originDomainID] = slotIndex;
    }

    /**
        @notice Executes a batch of deposit proposals using a specified handler contract for each proposal
        @notice Failed executeProposal from handler don't revert, emits {FailedHandlerExecution} event.
        @param proposals Array of Proposal which consists of:
        - originDomainID ID of chain deposit originated from.
        - resourceID ResourceID to be used when making deposits.
        - depositNonce ID of deposit generated by origin Bridge contract.
        - data Data originally provided when deposit was made.
        @notice Emits {ProposalExecution} event for each proposal in the batch.
     */
    function executeProposals(
        Proposal[] memory proposals,
        bytes[] memory accountProof,
        uint256 slot
    ) public whenBridgeNotPaused {
        if (proposals.length == 0) revert EmptyProposalsArray();

        for (uint256 i = 0; i < proposals.length; i++) {
            if (isProposalExecuted(proposals[i].originDomainID, proposals[i].depositNonce)) {
                continue;
            }
            bytes32 stateRoot;
            bytes32 storageRoot;
            address routerAddress = _originDomainIDToRouter[proposals[i].originDomainID];

            IStateRootStorage stateRootStorage = IStateRootStorage(_securityModels[proposals[i].securityModel]);
            stateRoot = stateRootStorage.getStateRoot(proposals[i].originDomainID, slot);
            storageRoot = StorageProof.getStorageRoot(accountProof, routerAddress, stateRoot);
            address handler = IBridge(_bridge)._resourceIDToHandlerAddress(proposals[i].resourceID);
            IHandler depositHandler = IHandler(handler);
            verify(proposals[i], storageRoot);

            usedNonces[proposals[i].originDomainID][proposals[i].depositNonce / 256] |=
                1 <<
                (proposals[i].depositNonce % 256);
            try depositHandler.executeProposal(proposals[i].resourceID, proposals[i].data) returns (
                bytes memory handlerResponse
            ) {
                emit ProposalExecution(proposals[i].originDomainID, proposals[i].depositNonce, handlerResponse);
            } catch (bytes memory lowLevelData) {
                emit FailedHandlerExecution(lowLevelData, proposals[i].originDomainID, proposals[i].depositNonce);
                usedNonces[proposals[i].originDomainID][proposals[i].depositNonce / 256] &= ~(1 <<
                    (proposals[i].depositNonce % 256));
                continue;
            }
        }
    }


    /**
        @notice Executes a deposit proposal using a specified handler contract
        @param proposal Proposal which consists of:
        - originDomainID ID of chain deposit originated from.
        - resourceID ResourceID to be used when making deposits.
        - depositNonce ID of deposit generated by origin Bridge contract.
        - data Data originally provided when deposit was made.
        @notice Emits {ProposalExecution} event.
     */
    function executeProposal(
        Proposal memory proposal,
        bytes[] memory accountProof,
        uint256 slot
    ) public {
        Proposal[] memory proposalArray = new Proposal[](1);
        proposalArray[0] = proposal;

        executeProposals(proposalArray, accountProof, slot);
    }

    /**
        @notice Returns a boolean value.
        @param domainID ID of chain deposit originated from.
        @param depositNonce ID of deposit generated by origin Bridge contract.
        @return Boolean value depending if deposit nonce has already been used or not.
     */
    function isProposalExecuted(uint8 domainID, uint256 depositNonce) public view returns (bool) {
        return usedNonces[domainID][depositNonce / 256] & (1 << (depositNonce % 256)) != 0;
    }

    /**
        @notice Checks if the submitted propsal has been verified.
        @param proposal Proposal which consists of:
        - originDomainID ID of chain deposit originated from.
        - resourceID ResourceID to be used when making deposits.
        - depositNonce ID of deposit generated by origin Bridge contract.
        - data Data originally provided when deposit was made.
        @return Boolean value depending if deposit nonce has already been used or not.
     */
    function verify(Proposal memory proposal, bytes32 storageRoot)
        internal
        returns (bool)
    {
        bytes32 transferHash;

        transferHash = keccak256(
            abi.encode(
                proposal.originDomainID,
                _domainID,
                proposal.securityModel,
                proposal.depositNonce,
                proposal.resourceID,
                keccak256(proposal.data)
            )
        );
        bytes32 slotKey = keccak256(abi.encode(keccak256(
            abi.encode(proposal.depositNonce, keccak256(abi.encode(_domainID, _slotIndexes[proposal.originDomainID])))
        )));

        bytes32 slotValue = StorageProof.getStorageValue(slotKey, storageRoot, proposal.storageProof);
        if (slotValue != transferHash) {
            revert TransferHashDoesNotMatchSlotValue(transferHash);
        }
        return true;
    }
}
